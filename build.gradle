plugins {
    id 'java'
    id 'jacoco'
    id 'com.auth0.gradle.oss-library.java'
}

repositories {
    mavenCentral()
}

group 'com.auth0'

logger.lifecycle("Using version ${version} for ${name} group $group")

def signingKey = findProperty('signingKey')
def signingKeyPwd = findProperty('signingPassword')

oss {
    name 'mvc-auth-commons'
    repository 'auth0-java-mvc-common'
    organization 'auth0'
    description 'Java library that simplifies the use of Auth0 for server-side MVC web apps'
    baselineCompareVersion '1.5.0'
    skipAssertSigningConfiguration true

    developers {
        auth0 {
            displayName = 'Auth0'
            email = 'oss@auth0.com'
        }
        lbalmaceda {
            displayName = 'Luciano Balmaceda'
            email = 'luciano.balmaceda@auth0.com'
        }
    }
}

signing {
    useInMemoryPgpKeys(signingKey, signingKeyPwd)
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    // Needed because of broken gradle metadata, see https://github.com/google/guava/issues/6612#issuecomment-1614992368
    sourceSets.all {
        configurations.getByName(runtimeClasspathConfigurationName) {
            attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
        }
        configurations.getByName(compileClasspathConfigurationName) {
            attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
        }
    }
}

compileJava {
    sourceCompatibility '1.8'
    targetCompatibility '1.8'
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
        exceptionFormat "short"
    }
}

dependencies {
    implementation 'javax.servlet:javax.servlet-api:3.1.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.google.guava:guava-annotations:r03'
    implementation 'commons-codec:commons-codec:1.15'

    api 'com.auth0:auth0:1.45.1'
    api 'com.auth0:java-jwt:3.19.4'
    api 'com.auth0:jwks-rsa:0.22.1'

    testImplementation "org.bouncycastle:bcprov-jdk15on:1.70"
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.mockito:mockito-core:2.8.9'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.springframework:spring-test:5.2.19.RELEASE'
    testImplementation 'com.squareup.okhttp3:okhttp:4.11.0'
}