plugins {
    id 'java'
    id 'jacoco'
    id 'com.auth0.gradle.oss-library.java'
}

repositories {
    mavenCentral()
}

group 'com.auth0'

logger.lifecycle("Using version ${version} for ${name} group $group")

oss {
    name 'mvc-auth-commons'
    repository 'auth0-java-mvc-common'
    organization 'auth0'
    description 'Java library that simplifies the use of Auth0 for server-side MVC web apps'
    baselineCompareVersion '1.5.0'

    developers {
        auth0 {
            displayName = 'Auth0'
            email = 'oss@auth0.com'
        }
        lbalmaceda {
            displayName = 'Luciano Balmaceda'
            email = 'luciano.balmaceda@auth0.com'
        }
    }
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    sourceCompatibility '17'
    targetCompatibility '17'
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
        exceptionFormat "short"
    }
}

dependencies {
    implementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.google.guava:guava-annotations:r03'
    implementation 'commons-codec:commons-codec:1.15'

    api 'com.auth0:auth0:1.45.1'
    api 'com.auth0:java-jwt:3.19.4'
    api 'com.auth0:jwks-rsa:0.22.1'

    testImplementation 'org.bouncycastle:bcprov-jdk15on:1.64'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    // https://mvnrepository.com/artifact/org.mockito/mockito-core
testImplementation 'org.mockito:mockito-core:4.11.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
  // https://mvnrepository.com/artifact/org.springframework/spring-test
testImplementation 'org.springframework:spring-test:6.0.14'

testImplementation 'org.springframework:spring-web:6.0.14'
    testImplementation 'com.squareup.okhttp3:okhttp:4.11.0'
}

// Creates a version.txt file containing the current version of the SDK.
// This file is picked up and parsed by our Ship Orb to determine the version.
task exportVersion()  {
    doLast {
        new File(rootDir, "version.txt").text = "$version"
    }
}
